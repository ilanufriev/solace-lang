plugins {
    alias(libs.plugins.kotlin.jvm)

    id 'application'
    id 'antlr'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.2'
    implementation 'org.antlr:antlr4-runtime:4.13.2'

    // Use the Kotlin Test integration.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'solace.compiler.AppKt'
}

sourceSets {
    main {
        java.srcDir("$buildDir/generated-src/antlr/main")
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.withType(org.gradle.api.plugins.antlr.AntlrTask).configureEach {
    // Generate sources into the desired package instead of the default (no package).
    arguments += ['-package', 'solace.compiler.antlr']
}

tasks.named('compileKotlin') {
    // Ensure ANTLR sources are generated before compiling Kotlin code that depends on them.
    dependsOn(tasks.named('generateGrammarSource'))
}
